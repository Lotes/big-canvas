### BASIC TYPES ###
typedef Size = integer[0...]
typedef Point = tuple of (integer, integer)
typedef BigInteger = string /^-?[0-9]+$/
typedef BigPoint = tuple of (BigInteger, BigInteger)
typedef StrokeOpacity = number[0...1]
typedef StrokeWidth = integer[1...100]
typedef Color = string /^#[0-9A-Fa-f]{6}$/
typedef Selection = nullable string
typedef LayerId = integer[0...5)

### TILES ###
typedef TileLocation = tuple of (BigInteger, BigInteger, LayerId) #(col, row, layer)

### ACTIONS ###
typedef ActionId = BigInteger
abstract struct ActionStartInfo {}
abstract struct DrawActionStartInfo: ActionStartInfo {
  selection: Selection;
}
abstract struct StrokeActionStartInfo: DrawActionStartInfo {
  startPoint: BigPoint;
  opacity: StrokeOpacity;
  width: StrokeWidth;
  layer: LayerId;
}
struct EraserActionStartInfo: StrokeActionStartInfo {}
struct BrushActionStartInfo: StrokeActionStartInfo {
  color: Color;	
}
struct FillActionStartInfo: DrawActionStartInfo  {
  color: Color;
}
struct Action {
  startInfo: ActionStartInfo;
  nextPoints: list of Point;	
  userId: UserId;
}

### USERS ###
typedef UserId = BigInteger
typedef UserWindowWidth = integer[1...4096]
typedef UserWindowHeight = integer[1...4096]
typedef UserWindow = tuple of (BigInteger, BigInteger, UserWindowWidth, UserWindowHeight) #x,y,w,h
typedef UserName = string
typedef UserInfo = tuple of (UserId, UserName, Color)

### SITES ###
enum SiteType {
  READ_ONLY,
  WRITABLE
}
typedef SiteId = string[...40] /^[a-zA-Z0-9]+$/

### CLIENTS ###
typedef ClientId = BigInteger

### REMOTE PROCEDURE CALL INTERFACES ###
interface PersistingWorker {
  function persistAction(userId: UserId, window: nullable UserWindow, startInfo: ActionStartInfo, nextPoints: list of BigPoint): ActionId;
}

interface MainWorker {
  #session
  event initialized(clientId: ClientId, userId: UserId);
  function setSite(siteId: SiteId): SiteType;
  
  #window
  function setWindow(window: UserWindow);
  event windowChanged(clientId: ClientId, window: nullable UserWindow);

  #users
  function resolveClientId(clientId: ClientId): UserId;
  function getUserByUserId(userId: UserId): UserInfo;

  ###
  #actions
  function beginAction(startInfo: ActionStartInfo);
  function doActionMove(point: BigPoint);
  function commitAction(): ActionId;
  function cancelAction();
  event onActionBegin(startingClientId: ClientId, startInfo: ActionStartInfo);
  event onActionMoved(movingClientId: ClientId, point: BigPoint);
  event onActionCommitted(committingClientId: ClientId, action: Action);
  event onActionCancelled(cancellingClientId: ClientId);
  function undo(actionId: nullable ActionId); #actionId=null means users last action
  function redo(actionId: nullable ActionId);
  ###
}