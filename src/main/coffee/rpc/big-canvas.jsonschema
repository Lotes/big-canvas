### BASIC TYPES ###
typedef Size = integer[0...]
typedef Point = tuple of (integer, integer)
typedef BigInteger = string /^-?[0-9]+$/
typedef BigPoint = tuple of (BigInteger, BigInteger)
typedef StrokeOpacity = number[0...1]
typedef StrokeWidth = integer[1...100]
typedef Color = string /^#[0-9A-Fa-f]{6}$/
typedef Selection = nullable string
typedef LayerId = integer[0...5)

### TILES ###
typedef TileLocation = tuple of (BigInteger, BigInteger, LayerId) #(col, row, layer)

### ACTIONS ###
typedef ActionId = BigInteger
abstract struct ActionStartInfo {}
abstract struct DrawActionStartInfo: ActionStartInfo {
  selection: Selection;
}
abstract struct StrokeActionStartInfo: DrawActionStartInfo {
  startPoint: BigPoint;
  opacity: StrokeOpacity;
  width: StrokeWidth;
  layer: LayerId;
}
struct EraserActionStartInfo: StrokeActionStartInfo {}
struct BrushActionStartInfo: StrokeActionStartInfo {
  color: Color;	
}
struct FillActionStartInfo: DrawActionStartInfo  {
  color: Color;
}
struct Action {
  startInfo: ActionStartInfo;
  nextPoints: list of Point;	
  userId: UserId;
}

### USERS ###
typedef UserId = BigInteger
typedef UserWindowWidth = integer[1...4096]
typedef UserWindowHeight = integer[1...4096]
struct UserWindow {
	x: BigInteger;
	y: BigInteger;
	width: UserWindowWidth;
	height: UserWindowHeight;
}

### SITES ###
enum SiteType {
	READ_ONLY,
	WRITEABLE
}

typedef SiteId = string[...40] /^[a-zA-Z0-9]+$/

### CLIENTS ###
typedef ClientId = BigInteger
typedef ClientList = list of ClientId

### REMOTE PROCEDURE CALL INTERFACES ###
interface AwarenessWorker {
  #session	
  function login(clientId: ClientId, userId: UserId, siteType: SiteType, siteId: SiteId);
  function logout(clientId: ClientId);	
	
  #window
  function setWindow(clientId: ClientId, window: UserWindow);
  event windowChanged(clientIds: ClientList, changedClientId: ClientId, window: nullable UserWindow);

  #actions
  function beginAction(clientId: ClientId, startInfo: ActionStartInfo);
  event onActionBegan(clientIds: ClientList, startingClientId: ClientId, startInfo: ActionStartInfo);
  function doActionMove(clientId: ClientId, point: BigPoint);
  event onActionMoved(clientIds: ClientList, movingClientId: ClientId, point: BigPoint);
  function commitAction(clientId: ClientId): ActionId;
  event onActionCommitted(clientIds: ClientList, committingClientId: ClientId, action: Action);
  function cancelAction(clientIds: ClientList);
  event onActionCancelled(clientIds: ClientList, cancellingClientId: ClientId);
}

interface MainWorker {
  #session
  function login(siteType: SiteType, siteId: SiteId);
  
  #window
  function setWindow(window: UserWindow);
  event windowChanged(clientId: ClientId, window: nullable UserWindow);
  
  #actions
  function beginAction(startInfo: ActionStartInfo);
  event onActionBegan(startingClientId: ClientId, startInfo: ActionStartInfo);
  function doActionMove(point: BigPoint);
  event onActionMoved(movingClientId: ClientId, point: BigPoint);
  function commitAction(): ActionId;
  event onActionCommitted(committingClientId: ClientId, action: Action);
  function cancelAction();
  event onActionCancelled(cancellingClientId: ClientId);	
}